module GoStatix

imports
  Common
  Declarations
  Statements

context-free start-symbols
  
  SourceFile

// Source file
sorts SourceFile SourceFile-Imports SourceFile-Decls
context-free syntax

  SourceFile.SourceFile = <
    <PackageClause>;

    <SourceFile-Imports*>

    <SourceFile-Decls*>
  >
  SourceFile-Imports = <<ImportDecl>;>
  SourceFile-Decls   = <<TopLevelDecl>;>

// Package clause
sorts PackageClause
context-free syntax

  PackageClause.PackageClause = <package <ID>>

// Import declarations
sorts ImportDecl MultiImportDecl-Inner ImportSpec ImportSpec-Qual ImportPath
context-free syntax

  ImportDecl.SingleImportDecl = <import <ImportSpec>>
  ImportDecl.MultiImportDecl  = <import (<MultiImportDecl-Inner*>)>
  MultiImportDecl-Inner       = <<ImportSpec>;>

  ImportSpec.ImportSpec  = <<ImportSpec-Qual> <ImportPath>>
  ImportSpec-Qual.Qual   = <>
  ImportSpec-Qual.Unqual = <.>
  ImportSpec-Qual.Alias  = ID
  ImportPath             = StringLit

// Source files for testing
sorts TestSources TestSource
context-free syntax
  
  TestSources.TestSources = TestSource*

  TestSource.SourceFile = <
    [<ImportPath>]
    <PackageClause>;

    <SourceFile-Imports*>
    <SourceFile-Decls*>
  >
