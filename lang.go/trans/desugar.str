module desugar

imports

  libspoofax/resource/path
  libspoofax/term/origin

  signatures/-

signature
  constructors
    SourceFile : ImportPath * PackageName * ImportDecls * TopLevelDecls -> SourceFile
    Elements   : ElementList -> LiteralValue-Inner
    Parameters : ParameterList -> Parameters-Inner

rules

  desugar-all =
    try(desugar-sourcefile);
    desugar-disambs;
    desugar-alts;
    desugar-short-var-decls;
    desugar-single-decls;
    desugar-list-opt

rules

  desugar-sourcefile:
      file@SourceFile(name, imports, decls) -> SourceFile(pkg-path, name, imports, decls)
    with file-path := <origin-file> file;
         pkg-path  := <current-language-relative-source-or-include-path;dirname> file-path

rules

  desugar-short-var-decls = topdown(try(desugar-short-var-decl))

  // ShortVarDecl
  desugar-short-var-decl: ShortVarDecl(ids, es)   -> SingleVarDecl(VarSpecNoType(ids, es))

rules

  desugar-single-decls = topdown(try(desugar-single-decl))

  // ImportDecl
  desugar-single-decl: SingleImportDecl(i)        -> ImportDecl([i])

  // ConstDecl
  desugar-single-decl: SingleConstDecl(c)         -> ConstDecl([c])

  // TypeDecl
  desugar-single-decl: SingleTypeDecl(t)          -> TypeDecl([t])

  // VarDecl
  desugar-single-decl: SingleVarDecl(v)           -> VarDecl([v])

rules

  desugar-list-opts = topdown(try(desugar-list-opt))

  // ParameterList-OPT
  desugar-list-opt: NoParameters()                -> []

  // IDList-OPT
  desugar-list-opt: NoIDs()                       -> []

  // ExprList-OPT
  desugar-list-opt: NoExprs()                     -> []

rules

  desugar-disambs = topdown(try(desugar-disamb))

  // QualifiedIdent
  desugar-disamb: QualifiedIdent-DISAMB(p, i)     -> QualifiedIdent(p, i)

  // PrimaryExpr
  desugar-disamb: Selector-DISAMB(e, i) 		      -> Selector(e, i)
  desugar-disamb: Call-DISAMB(e, args)            -> Call(e, args)

  // MethodExpr
  desugar-disamb: MethodExpr-DISAMB(e, m)         -> MethodExpr(e, m)

  // Conversion
  desugar-disamb: Conversion-DISAMB(t, e)         -> Conversion(t, e)
  desugar-disamb: Conversion-DISAMB-ALT(t, e)     -> Conversion-ALT(t, e)

rules

  desugar-alts = topdown(try(desugar-alt))

  // LiteralValue
  desugar-alt: LiteralValue-ALT(es)               -> LiteralValue(es)

  // Arguments-OPT
  desugar-alt: Arguments-ALT(as) 			            -> Arguments(as)
  desugar-alt: ArgumentsExplode-ALT(as) 		      -> ArgumentsExplode(as)

  // Conversion
  desugar-alt: Conversion-ALT(t, e)               -> Conversion(t, e)

