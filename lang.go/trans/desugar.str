module desugar

imports

  libspoofax/resource/path
  libspoofax/term/origin

  signatures/-

signature
  constructors
    SourceFile2: ImportPath * PackageName * ImportDecls * TopLevelDecls -> SourceFile
    Elements   : ElementList -> LiteralValue-Inner
    Parameters : ParameterList -> Parameters-Inner

rules

  desugar-all = topdown(try(
    desugar-sourcefile <+
    desugar-disamb <+
    desugar-alt <+
    desugar-short-var-decl <+
    desugar-single-decl <+
    desugar-list-opt
  ))

rules

  desugar-sourcefile:
      file@SourceFile(name, imports, decls) -> SourceFile2(pkg-path, name, imports, decls)
    with file-path := <origin-file> file;
         pkg-path  := <current-language-relative-source-or-include-path;dirname> file-path

rules

  desugar-short-var-decl: ShortVarDecl(ids, es)         -> SingleVarDecl(VarSpecNoType(ids, es))

rules

  desugar-single-decl: SingleImportDecl(i)              -> ImportDecl([i])
  desugar-single-decl: SingleConstDecl(c)               -> ConstDecl([c])
  desugar-single-decl: SingleTypeDecl(t)                -> TypeDecl([t])
  desugar-single-decl: SingleVarDecl(v)                 -> VarDecl([v])

rules

  desugar-list-opt: NoIDs()                             -> []
  desugar-list-opt: NoExprs()                           -> []

rules

  desugar-disamb: QualifiedIdent-DISAMB(p, i)           -> QualifiedIdent(p, i)
  desugar-disamb: Selector-DISAMB(e, i) 		            -> Selector(e, i)
  desugar-disamb: Call-DISAMB(e, args)                  -> Call(e, args)
  desugar-disamb: MethodExpr-DISAMB(e, m)               -> MethodExpr(e, m)
  desugar-disamb: Conversion-DISAMB(t, e)               -> Conversion(t, e)
  desugar-disamb: Conversion-DISAMB-ALT(t, e)           -> Conversion-ALT(t, e)

rules

  desugar-alt: LiteralValue-ALT(es)                     -> LiteralValue(es)
  desugar-alt: Arguments-ALT(as) 			                  -> Arguments(as)
  desugar-alt: ArgumentsExplode-ALT(as) 		            -> ArgumentsExplode(as)
  desugar-alt: Conversion-ALT(t, e)                     -> Conversion(t, e)
  desugar-alt: Parameters-ALT(ps)                       -> Parameters(ps)
  desugar-alt: CallOrConversion-ALT(t, e)               -> CallOrConversion(t, e)
