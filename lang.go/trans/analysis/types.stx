module analysis/types

imports
  analysis/signatures
  // signatures/Blocks-sig
  // signatures/Declarations-sig
  // signatures/Expressions-sig
  // signatures/GoLang-sig
  // signatures/Lexical-sig
  // signatures/Packages-sig
  // signatures/Representation-sig
  // signatures/SortNames-sig
  // signatures/Statements-sig
  // signatures/Types-sig

imports
  analysis/expressions
  analysis/util
  analysis/name-resolution

signature
  sorts FAMILY
  constructors
    INT_FAMILY      : FAMILY
    FLOAT_FAMILY    : FAMILY
    COMPLEX_FAMILY  : FAMILY
    BOOL_FAMILY     : FAMILY
    STRING_FAMILY   : FAMILY
    OTHER_FAMILY    : TYPE -> FAMILY

rules familyOfType : TYPE -> FAMILY

  familyOfType(UNTYPED_BOOL())    = BOOL_FAMILY().
  familyOfType(BOOL())            = BOOL_FAMILY().

  familyOfType(UNTYPED_STRING())  = STRING_FAMILY().
  familyOfType(STRING())          = STRING_FAMILY().

  // UNTYPED_RUNE?
  familyOfType(UNTYPED_INT())     = INT_FAMILY().

  familyOfType(INT())             = INT_FAMILY().
  familyOfType(INT8())            = INT_FAMILY().
  familyOfType(INT16())           = INT_FAMILY().
  familyOfType(INT32())           = INT_FAMILY().
  familyOfType(INT64())           = INT_FAMILY().

  familyOfType(UINT())            = INT_FAMILY().
  familyOfType(UINT8())           = INT_FAMILY().
  familyOfType(UINT16())          = INT_FAMILY().
  familyOfType(UINT32())          = INT_FAMILY().
  familyOfType(UINT64())          = INT_FAMILY().

  familyOfType(UNTYPED_FLOAT())   = FLOAT_FAMILY().
  familyOfType(FLOAT32())         = FLOAT_FAMILY().
  familyOfType(FLOAT64())         = FLOAT_FAMILY().

  familyOfType(UNTYPED_COMPLEX()) = COMPLEX_FAMILY().
  familyOfType(COMPLEX64())       = COMPLEX_FAMILY().
  familyOfType(COMPLEX128())      = COMPLEX_FAMILY().

  familyOfType(T)                 = OTHER_FAMILY(T).


signature
  sorts TYPE
  // Special types
  constructors
    NAMED       : occurrence -> TYPE
    FUN         : list(TYPE) * TYPE -> TYPE
    ARRAY       : int * TYPE -> TYPE
    SLICE       : TYPE -> TYPE
    STRUCT      : scope -> TYPE
    PTR         : TYPE -> TYPE
    MAP         : TYPE * TYPE -> TYPE
    // A TUPLE type, to model Go functions with multiple return values.
    // Note that tuples don't have first-class support in Go. For example,
    // you cannot nest tuples. But this way we can distinguish between the case
    // of a function with zero, one, and multiple return values.
    // Invariants: empty and singleton tuples are not allowed; nested tuples are not allowed.
    TUPLE       : list(TYPE) -> TYPE
    // A VOID type, to model Go functions with no return value.
    VOID        : TYPE

    CHANNEL     : TYPE -> TYPE
    // A type, which is the type of a TYPE
    TYPE        : TYPE -> TYPE

  // Untyped constant types
  constructors
    UNTYPED_BOOL      : TYPE
    UNTYPED_STRING    : TYPE
    UNTYPED_RUNE      : TYPE
    UNTYPED_INT       : TYPE
    UNTYPED_FLOAT     : TYPE
    UNTYPED_COMPLEX   : TYPE

  // Built-in types
  constructors
    BOOL        : TYPE
    STRING      : TYPE
    ERROR       : TYPE

    INT         : TYPE
    INT8        : TYPE
    INT16       : TYPE
    INT32       : TYPE    // RUNE
    INT64       : TYPE

    UINT        : TYPE
    UINT8       : TYPE    // BYTE
    UINT16      : TYPE
    UINT32      : TYPE
    UINT64      : TYPE
    UINTPTR     : TYPE

    FLOAT32     : TYPE
    FLOAT64     : TYPE

    COMPLEX64   : TYPE
    COMPLEX128  : TYPE


rules typeOfType : scope * Type -> TYPE

  typeOfType(s, TypeName2Type(typeName)) = T :-
    typeOfTypeName(s, typeName) == T.

  typeOfType(s, PointerType(type)) = PTR(T) :-
    typeOfType(s, type) == T.

  typeOfType(s, ArrayType(lengthExpr, type)) = ARRAY(/* dummy size */ 42, T) :-
    // TODO: Evaluate constant expression lengthExpr to an int?
    typeOfType(s, type) == T.

  typeOfType(s, SliceType(type)) = SLICE(T) :-
    typeOfType(s, type) == T.

  typeOfType(s, StructType(elements)) = STRUCT(s_struct) :- {T_ELEMENTS}
    new s_struct,
//    typesOfStructElements(s_struct, elements) == T_ELEMENTS.
    false | error $[typeOfType(StructType) not implemented.].

  typeOfType(s, FunctionType(sig)) = TFUN :-
    typeOfSignature(s, sig) == TFUN.

  typeOfType(s, InterfaceType(members)) = T :-
    // TODO
    false | error $[typeOfType(InterfaceType) not implemented.].

  typeOfType(s, MapType(keyType, elementType)) = MAP(TKEY, TELEMENT) :-
    typeOfType(s, keyType) == TKEY,
    typeOfType(s, elementType) == TELEMENT.

  typeOfType(s, BidirectionalChannelType(type)) = T :-
    // TODO
    false | error $[typeOfType(BidirectionalChannelType) not implemented.].

  typeOfType(s, SendingChannelType(type)) = T :-
    // TODO
    false | error $[typeOfType(SendingChannelType) not implemented.].

  typeOfType(s, ReceivingChannelType(type)) = T :-
    // TODO
    false | error $[typeOfType(ReceivingChannelType) not implemented.].


//rules typeOfStructElement : scope * StructElement -> TYPE
//
//  typesOfStructElements maps typeOfStructElement(*, list(*)) = list(*)
//
//  typeOfStructElement(_, _) = T :-
//    true.
//
////  typeOfStructElement(s_struct, StructElement(field)) = T :-
////    typeOfField(s_struct, field) == T.
////
////  typeOfStructElement(s_struct, StructElementTagged(field, _)) = T :-
////    typeOfField(s_struct, field) == T.
rules typeOfTypeName : scope * TypeName -> TYPE

  typeOfTypeName(s, TypeName(name)) = T :-
    // FIXME:
    resolveType(s, name) == T.

  typeOfTypeName(s, QualifiedTypeName(x, y)) = T :-
    // TODO
    false | error $[typeOfType(QualifiedTypeName) not implemented.].

  typeOfTypeName(s, Bool()) = BOOL().
  typeOfTypeName(s, String()) = STRING().
  typeOfTypeName(s, Error()) = ERROR().

  typeOfTypeName(s, Int()) = INT().
  typeOfTypeName(s, Int8()) = INT8().
  typeOfTypeName(s, Int16()) = INT16().
  typeOfTypeName(s, Int32()) = INT32().
  typeOfTypeName(s, Int64()) = INT64().

  typeOfTypeName(s, UInt()) = UINT().
  typeOfTypeName(s, UInt8()) = UINT8().
  typeOfTypeName(s, UInt16()) = UINT16().
  typeOfTypeName(s, UInt32()) = UINT32().
  typeOfTypeName(s, UInt64()) = UINT64().
  typeOfTypeName(s, UIntPtr()) = UINTPTR().

  typeOfTypeName(s, Float32()) = FLOAT32().
  typeOfTypeName(s, Float64()) = FLOAT64().

  typeOfTypeName(s, Complex64()) = COMPLEX64().
  typeOfTypeName(s, Complex128()) = COMPLEX128().

  typeOfTypeName(s, Byte()) = UINT8().
  typeOfTypeName(s, Rune()) = INT32().


rules typeOfSignature : scope * Signature -> TYPE

  typeOfSignature(s, Signature(parameters, result)) = FUN(TPARAMS, TRES) :-
    typesOfParameters(s, parameters) == TPARAMS,
    typeOfResult(s, result) == TRES.


rules typeOfResult : scope * Result -> TYPE

  typeOfResult(s, NoResult()) = VOID.

  typeOfResult(s, Parameters2Result(parameters)) = TUPLE(TRES) :-
    typesOfParameters(s, parameters) == TRES.

  typeOfResult(s, Type2Result(type)) = T :-
    typeOfType(s, type) == T.


rules typesOfParameters : scope * Parameters -> list(TYPE)

  typesOfParameters(s, Parameters(parameterDecls)) = TS :-
    typesOfParameterDecls(s, parameterDecls) == TS.


rules typeOfParameterDecl : scope * ParameterDecl -> TYPE

  typesOfParameterDecls maps typeOfParameterDecl(*, list(*)) = list(*)

  typeOfParameterDecl(s, ParameterDecl(_, type)) = T :-
    typeOfType(s, type) == T.

  typeOfParameterDecl(s, VariadicParameterDecl(_, type)) = T :-
    // TODO
    false | error $[typeOfParameterDecl(VariadicParameterDecl) not implemented.].


rules typeOfInterfaceTypeMember : scope * InterfaceTypeMember -> TYPE

  typeOfInterfaceTypeMember(s, MethodSpec(name, sig)) = T :-
    // TODO
    false | error $[typeOfInterfaceTypeMember(MethodSpec) not implemented.].

  typeOfInterfaceTypeMember(s, EmbeddedInterface(typeName)) = T :-
    // TODO
    false | error $[typeOfInterfaceTypeMember(EmbeddedInterface) not implemented.].

rules isIntType : TYPE

  isIntType(T) :-
    familyOfType(T) == INT_FAMILY().

rules isBoolType : TYPE

  isBoolType(T) :-
    familyOfType(T) == BOOL_FAMILY().

rules isArithmeticType : TYPE

  isArithmeticType(T) :- {F}
    familyOfType(T) == F,
    isArithmeticTypeFamily(F).

rules isArithmeticTypeFamily : FAMILY

  isArithmeticTypeFamily(INT_FAMILY()).
  isArithmeticTypeFamily(FLOAT_FAMILY()).
  isArithmeticTypeFamily(COMPLEX_FAMILY()).

rules isArithmeticOrStringType : TYPE

  isArithmeticOrStringType(T) :- {F}
    familyOfType(T) == F,
    isArithmeticOrStringTypeFamily(F).

rules isArithmeticOrStringTypeFamily : FAMILY

  isArithmeticOrStringTypeFamily(STRING_FAMILY()).
  isArithmeticOrStringTypeFamily(F) :- isArithmeticTypeFamily(F).

// Coerces an untyped (constant) type into a typed type
// or returns the given type when it was already typed.
rules ensureTyped : TYPE -> TYPE

  ensureTyped(UNTYPED_BOOL())    = BOOL().
  ensureTyped(UNTYPED_STRING())  = STRING().
  ensureTyped(UNTYPED_RUNE())    = INT32().
  ensureTyped(UNTYPED_INT())     = INT().
  ensureTyped(UNTYPED_FLOAT())   = FLOAT64().
  ensureTyped(UNTYPED_COMPLEX()) = COMPLEX128().
  ensureTyped(T)                 = T.

//// Fails when the given type is not an untyped (constant) type
//rules isUntyped : TYPE
//
//  isUntyped(UNTYPED_BOOL()).
//  isUntyped(UNTYPED_STRING()).
//  isUntyped(UNTYPED_RUNE()).
//  isUntyped(UNTYPED_INT()).
//  isUntyped(UNTYPED_FLOAT()).
//  isUntyped(UNTYPED_COMPLEX()).

//// Families of types that can be compared for equality
//rules isEquatableTo : FAMILY * FAMILY
//
//  isEquatableTo(BOOL_FAMILY(),    BOOL_FAMILY()).
//  isEquatableTo(INT_FAMILY(),     INT_FAMILY()).
//  isEquatableTo(FLOAT_FAMILY(),   FLOAT_FAMILY()).
//  isEquatableTo(COMPLEX_FAMILY(), COMPLEX_FAMILY()).
//  isEquatableTo(STRING_FAMILY(),  STRING_FAMILY()).
//  // TODO: pointers
//  // TODO: Channels
//  // TODO: interfaces
//  // TODO: A value x of non-interface type X and a value t of interface type T are comparable when values of type X are comparable and X implements T. They are equal if t's dynamic type is identical to X and t's dynamic value is equal to x.
//  isEquatableTo(OTHER_FAMILY(T1), OTHER_FAMILY(T2)) :- T1 == T2.
//  // TODO: structs iff all their fields are comparable
//
//  isEquatableTo(OTHER_FAMILY(ARRAY(_, T1)), OTHER_FAMILY(ARRAY(_, T2))) :-
//    isEquatableTo(T1, T2).
//
//// Families of types that can be compared for order
//rules isComparable : FAMILY * FAMILY
//
//  isComparable(INT_FAMILY(),      INT_FAMILY()).
//  isComparable(FLOAT_FAMILY(),    FLOAT_FAMILY()).
//  isComparable(STRING_FAMILY(),   STRING_FAMILY()).
//  isComparable(OTHER_FAMILY(T1),  OTHER_FAMILY(T2)) :- T1 == T2.


//rules defaultTypeOfUntypedType : TYPE -> TYPE

//  defaultTypeOfUntypedType(UNTYPED_INT)     = INT.
//  defaultTypeOfUntypedType(UNTYPED_BOOL)    = BOOL.
//  defaultTypeOfUntypedType(UNTYPED_RUNE)    = INT32.
//  defaultTypeOfUntypedType(UNTYPED_FLOAT)   = FLOAT64.
//  defaultTypeOfUntypedType(UNTYPED_COMPLEX) = COMPLEX128.
//  defaultTypeOfUntypedType(UNTYPED_STRING)  = STRING.



  // TODO
//rules lookupPkg : scope * string -> PKG
//
//  lookupPkg(s, x) = P :- {dx}
//    @x.ref := dx
//    typeOfPkg of Pkg{x} in s |-> [(_, (Pkg{dx}, T))].

// Whether the left type is assignable to a variable of the right type
rules isAssignableTo : TYPE * TYPE

  isAssignableTo(T, T).

  isAssignableTo(UNTYPED_BOOL(),    BOOL()).
  isAssignableTo(UNTYPED_STRING(),  STRING()).

  isAssignableTo(UNTYPED_RUNE(),    INT()).
  isAssignableTo(UNTYPED_RUNE(),    INT8()).
  isAssignableTo(UNTYPED_RUNE(),    INT16()).
  isAssignableTo(UNTYPED_RUNE(),    INT32()).
  isAssignableTo(UNTYPED_RUNE(),    INT64()).
  isAssignableTo(UNTYPED_RUNE(),    UINT()).
  isAssignableTo(UNTYPED_RUNE(),    UINT8()).
  isAssignableTo(UNTYPED_RUNE(),    UINT16()).
  isAssignableTo(UNTYPED_RUNE(),    UINT32()).
  isAssignableTo(UNTYPED_RUNE(),    UINT64()).
  isAssignableTo(UNTYPED_RUNE(),    UINTPTR()).
  isAssignableTo(UNTYPED_RUNE(),    FLOAT32()).
  isAssignableTo(UNTYPED_RUNE(),    FLOAT64()).
  isAssignableTo(UNTYPED_RUNE(),    COMPLEX64()).
  isAssignableTo(UNTYPED_RUNE(),    COMPLEX128()).

  isAssignableTo(UNTYPED_INT(),     INT()).
  isAssignableTo(UNTYPED_INT(),     INT8()).
  isAssignableTo(UNTYPED_INT(),     INT16()).
  isAssignableTo(UNTYPED_INT(),     INT32()).
  isAssignableTo(UNTYPED_INT(),     INT64()).
  isAssignableTo(UNTYPED_INT(),     UINT()).
  isAssignableTo(UNTYPED_INT(),     UINT8()).
  isAssignableTo(UNTYPED_INT(),     UINT16()).
  isAssignableTo(UNTYPED_INT(),     UINT32()).
  isAssignableTo(UNTYPED_INT(),     UINT64()).
  isAssignableTo(UNTYPED_INT(),     UINTPTR()).
  isAssignableTo(UNTYPED_INT(),     FLOAT32()).
  isAssignableTo(UNTYPED_INT(),     FLOAT64()).
  isAssignableTo(UNTYPED_INT(),     COMPLEX64()).
  isAssignableTo(UNTYPED_INT(),     COMPLEX128()).

  isAssignableTo(UNTYPED_FLOAT(),   FLOAT32()).
  isAssignableTo(UNTYPED_FLOAT(),   FLOAT64()).
  isAssignableTo(UNTYPED_FLOAT(),   COMPLEX64()).
  isAssignableTo(UNTYPED_FLOAT(),   COMPLEX128()).

  isAssignableTo(UNTYPED_COMPLEX(), COMPLEX64()).
  isAssignableTo(UNTYPED_COMPLEX(), COMPLEX128()).


// Families of types that can be compared for equality
rules isEquatable : TYPE * TYPE

  isEquatable(T1, T2) :- {F1 F2}
    familyOfType(T1) == F1,
    familyOfType(T2) == F2,
    isFamilyEquatable(F1, F2).

rules isFamilyEquatable : FAMILY * FAMILY

  isFamilyEquatable(BOOL_FAMILY(),    BOOL_FAMILY()).
  isFamilyEquatable(INT_FAMILY(),     INT_FAMILY()).
  isFamilyEquatable(FLOAT_FAMILY(),   FLOAT_FAMILY()).
  isFamilyEquatable(COMPLEX_FAMILY(), COMPLEX_FAMILY()).
  isFamilyEquatable(STRING_FAMILY(),  STRING_FAMILY()).
  // TODO: pointers
  // TODO: Channels
  // TODO: interfaces
  // TODO: A value x of non-interface type X and a value t of interface type T are comparable when values of type X are comparable and X implements T. They are equal if t's dynamic type is identical to X and t's dynamic value is equal to x.
  isFamilyEquatable(OTHER_FAMILY(T1), OTHER_FAMILY(T2)) :- T1 == T2.
  // TODO: structs iff all their fields are comparable

  isFamilyEquatable(OTHER_FAMILY(ARRAY(_, T1)), OTHER_FAMILY(ARRAY(_, T2))) :-
    isEquatable(T1, T2).

rules isComparable : TYPE * TYPE

  isComparable(T1, T2) :- {F1 F2}
    familyOfType(T1) == F1,
    familyOfType(T2) == F2,
    isFamilyComparable(F1, F2).

// Families of types that can be compared for order
rules isFamilyComparable : FAMILY * FAMILY

  isFamilyComparable(INT_FAMILY(),      INT_FAMILY()).
  isFamilyComparable(FLOAT_FAMILY(),    FLOAT_FAMILY()).
  isFamilyComparable(STRING_FAMILY(),   STRING_FAMILY()).
  isFamilyComparable(OTHER_FAMILY(T1),  OTHER_FAMILY(T2)) :- T1 == T2.
