/**
 * Packages -- https://golang.org/ref/spec#Packages
 */
module Packages

imports

  Declarations
  Lexical
  SortNames

context-free start-symbols

  SourceFile

/**
 * Source file organization -- https://golang.org/ref/spec#Source_file_organization
 */
sorts SourceFile ImportDecl-ELEM TopLevelDecl-ELEM
context-free syntax

  SourceFile.SourceFile       = <<PackageClause>; <ImportDecl-ELEM*> <TopLevelDecl-ELEM*>>
  ImportDecl-ELEM             = <<ImportDecl>;>
  TopLevelDecl-ELEM           = <<TopLevelDecl>;>


/**
 * Package clause -- https://golang.org/ref/spec#Package_clause
 */
sorts PackageClause PackageName
context-free syntax

  PackageClause.PackageClause = <package <PackageName>>
  PackageName                 = ID


/**
 * Import declarations -- https://golang.org/ref/spec#Import_declarations
 */
sorts ImportDecl ImportSpec-ELEM ImportSpec ImportSpecQual ImportPath
context-free syntax

  ImportDecl.SingleImportDecl = <import <ImportSpec>>
  ImportDecl.ImportDecl       = <import (<ImportSpec-ELEM*>)>
  ImportSpec-ELEM             = <<ImportSpec>;>

  ImportSpec.ImportSpec       = <<ImportSpecQual> <ImportPath>>
  ImportSpecQual.Qual         = <>
  ImportSpecQual.Unqual       = <.>
  ImportSpecQual.Alias        = <<PackageName>>
  ImportPath                  = STRINGLIT

