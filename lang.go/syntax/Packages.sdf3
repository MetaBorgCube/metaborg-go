/**
 * Packages -- https://golang.org/ref/spec#Packages
 */
module Packages

imports

  Declarations
  Lexical
  SortNames

context-free start-symbols

  SourceFile

/**
 * Source file organization -- https://golang.org/ref/spec#Source_file_organization
 */
context-free sorts SourceFile
context-free syntax

  SourceFile.SourceFile                     = <package <ID>; <ImportDecl*> <TopLevelDecl*>>


/**
 * Import declarations -- https://golang.org/ref/spec#Import_declarations
 */
context-free sorts ImportDecl ImportSpec ImportSpecQual
context-free syntax

  ImportDecl.SingleImportDecl               = <import <ImportSpec>>
  ImportDecl.ImportDecl                     = <import (<ImportSpec*>);>

  ImportSpec.ImportSpec                     = <<ImportSpecQual> <STRING_LIT>;>
  ImportSpecQual.Qual                       = <>
  ImportSpecQual.Unqual                     = <.>
  ImportSpecQual.Alias                      = <<ID>>

