/**
 * Packages -- https://golang.org/ref/spec#Packages
 */
module Packages

imports

  Declarations
  Lexical

context-free start-symbols

  SourceFile

/**
 * Source file organization -- https://golang.org/ref/spec#Source_file_organization
 */
context-free syntax

  SourceFile.SourceFile       = <<PackageClause>; <ImportDecl-ELEM*> <TopLevelDecl-ELEM*>>
  ImportDecl-ELEM             = <<ImportDecl>;>
  TopLevelDecl-ELEM           = <<TopLevelDecl>;>


/**
 * Package clause -- https://golang.org/ref/spec#Package_clause
 */
context-free syntax

  PackageClause.PackageClause = <package <PackageName>>
  PackageName                 = ID


/**
 * Import declarations -- https://golang.org/ref/spec#Import_declarations
 */
context-free syntax

  ImportDecl.SingleImportDecl = <import <ImportSpec>>
  ImportDecl.ImportDecl       = <import (<ImportSpec-ELEM*>)>
  ImportSpec-ELEM             = <<ImportSpec>;>

  ImportSpec.ImportSpec       = <<ImportSpecQual> <ImportPath>>
  ImportSpecQual.Qual         = <>
  ImportSpecQual.Unqual       = <.>
  ImportSpecQual.Alias        = <<PackageName>>
  ImportPath                  = STRINGLIT

