module Expressions

imports

  Declarations
  Lexical
  Packages
  Representation
  Types

context-free start-symbols

  Expression

/**
 * Operands -- https://golang.org/ref/spec#Operands
 */
 
  context-free syntax
 
  Operand = Literal
  Operand = OperandName
  Operand = MethodExpr
  Operand = <(<Expression>)>

  Literal = BasicLit
  Literal = CompositeLit
  Literal = FunctionLit
  
  BasicLit.IntLit       = IntLit
  BasicLit.FloatLit     = FloatLit
  BasicLit.ImaginaryLit = ImaginaryLit
  BasicLit.RuneLit      = RuneLit
  BasicLit.StringLit    = StringLit

  OperandName.OperandName          = Identifier
  OperandName.QualifiedOperandName = QualifiedIdent


/**
 * Qualified identifiers -- https://golang.org/ref/spec#Qualified_identifiers
 */

  context-free syntax 

  QualifiedIdent.QualifiedIdent = [[PackageName].q#[Identifier]]
  QualifiedIdent.QualifiedIdent = <<PackageName>.<Identifier>>


/**
 * Composite literals -- https://golang.org/ref/spec#Composite_literals
 */

  context-free syntax 

  CompositeLit.CompositeLit = <<LiteralType> <LiteralValue>>

  LiteralType               = StructType
  LiteralType               = ArrayType
  LiteralType.AutoArrayType = <[ ... ]<ElementType>>
  LiteralType               = SliceType
  LiteralType               = MapType
  LiteralType               = TypeName

  LiteralValue.LiteralValue         = <{ <LiteralValue-Inner-OPT> }>
  LiteralValue-Inner-OPT.NoElements = <>
  LiteralValue-Inner-OPT.Elements   = <<ElementList> <Comma?>>

  ElementList                = {KeyedElement ","}+
  KeyedElement.KeyedElement  = <<KeyedElement-Key-OPT> <Element>>
  KeyedElement-Key-OPT.NoKey = <>
  KeyedElement-Key-OPT       = <<Key>:>

  Key                = FieldName
  Key                = Expression
  Key                = LiteralValue
  FieldName          = Identifier
  Element            = Expression
  Element            = LiteralValue


/**
 * Function literals -- https://golang.org/ref/spec#Function_literals
 */

  context-free syntax 

  FunctionLit.FunctionLit = <func <Function>>


/**
 * Primary expressions -- https://golang.org/ref/spec#Primary_expressions
 */
 
  context-free syntax 

  PrimaryExpr = Operand
  PrimaryExpr = Conversion
  
  // inlined
  PrimaryExpr.Selector      = [[PrimaryExpr].s#[Identifier]] {left}
  PrimaryExpr.Selector      = <<PrimaryExpr>.<Identifier>> {left}
  PrimaryExpr.Index         = <<PrimaryExpr>[<Expression>]>
  PrimaryExpr.Slice         = <<PrimaryExpr>[<Expression-OPT>:<Expression-OPT>]>
  PrimaryExpr.Slice         = <<PrimaryExpr>[<Expression-OPT>:<Expression-OPT>:<Expression-OPT>]>
  PrimaryExpr.TypeAssertion = <<PrimaryExpr>.(<Type>)>
  PrimaryExpr.Arguments     = [[PrimaryExpr](c#[Arguments-Inner-OPT])]
  PrimaryExpr.Arguments     = <<PrimaryExpr>(<Arguments-Inner-OPT>)>
  
  Arguments-Inner-OPT.NoArguments        = <>
  Arguments-Inner-OPT.Arguments          = <<Arguments-Inner-Inner> <Dots?> <Comma?>>
  Arguments-Inner-Inner.Arguments        = ExpressionList
  Arguments-Inner-Inner.TypeArgument     = Type
  Arguments-Inner-Inner.TypedArguments   = <<Type>, <ExpressionList>>


/**
 * Selectors -- https://golang.org/ref/spec#Selectors
 */

  // no extra syntax

/**
 * Method expressions -- https://golang.org/ref/spec#Method_expressions
 */

  context-free syntax 

  MethodExpr.MethodExpr        = [[ReceiverType].m#[MethodName]]
  MethodExpr.MethodExpr        = <<ReceiverType>.<MethodName>>
  ReceiverType.ValueReceiver   = TypeName
  ReceiverType.PointerReceiver = <(*<TypeName>)>
  ReceiverType                 = <(<ReceiverType>)> {bracket}


/**
 * Method values -- https://golang.org/ref/spec#Method_values
 */

  // no syntax

/**
 * Index expressions -- https://golang.org/ref/spec#Index_expressions
 */

  // no syntax

/**
 * Slice expressions -- https://golang.org/ref/spec#Slice_expressions
 */
 
  // no syntax

/**
 * Type assertions -- https://golang.org/ref/spec#Type_assertions
 */

  // no syntax

/**
 * Calls -- https://golang.org/ref/spec#Calls
 */
 
  // no syntax

/**
 * Passing arguments to ... parameters -- https://golang.org/ref/spec#Passing_arguments_to_..._parameters
 */
 
  // no syntax

/**
 * Operators -- https://golang.org/ref/spec#Operators
 */
 
  context-free syntax 

  Expression = UnaryExpr
  Expression = BinaryExpr

  Expression-OPT.NoExpression = <>
  Expression-OPT              = Expression

  // binary_op
  BinaryExpr.LogicalOr  = <<Expression> || <Expression>> {left}
  BinaryExpr.LogicalAnd = <<Expression> && <Expression>> {left}

  // rel_op
  BinaryExpr.Equal          = <<Expression> == <Expression>> {left}
  BinaryExpr.NotEqual       = <<Expression> != <Expression>> {left}
  BinaryExpr.Less           = [[Expression] < [Expression]] {left}
  BinaryExpr.LessOrEqual    = [[Expression] <= [Expression]] {left}
  BinaryExpr.Greater        = [[Expression] > [Expression]] {left}
  BinaryExpr.GreaterOrEqual = [[Expression] >= [Expression]] {left}

  // add_op
  BinaryExpr.Sum        = [[Expression] + [Expression]] {left}
  BinaryExpr.Difference = [[Expression] - [Expression]] {left}
  BinaryExpr.BitwiseOr  = [[Expression] | [Expression]] {left}
  BinaryExpr.BitwiseXor = [[Expression] ^ [Expression]] {left}

  // mul_op
  BinaryExpr.Product    = [[Expression] * [Expression]] {left}
  BinaryExpr.Quotient   = [[Expression] / [Expression]] {left}
  BinaryExpr.Remainder  = [[Expression] % [Expression]] {left}
  BinaryExpr.LeftShift  = [[Expression] << [Expression]] {left}
  BinaryExpr.RightShift = [[Expression] >> [Expression]] {left}
  BinaryExpr.BitwiseAnd = [[Expression] & [Expression]] {left}
  BinaryExpr.BitClear   = [[Expression] &^ [Expression]] {left}
  
  UnaryExpr                   = PrimaryExpr
  UnaryExpr.Positive          = <+<UnaryExpr>> {left}
  UnaryExpr.Negation          = <-<UnaryExpr>> {left}
  UnaryExpr.Not               = <!<UnaryExpr>> {left}
  UnaryExpr.BitwiseComplement = <^<UnaryExpr>> {left}
  UnaryExpr.AddressOf         = <*<UnaryExpr>> {left} // not clearly named in spec
  UnaryExpr.ValueAt           = <&<UnaryExpr>> {left} // not clearly named in spec
  UnaryExpr.Recieve           = [<-[UnaryExpr]] {left}

  context-free priorities
  { left: // unary_op
    UnaryExpr.Positive
    UnaryExpr.Negation
    UnaryExpr.Not
    UnaryExpr.BitwiseComplement
    UnaryExpr.AddressOf
    UnaryExpr.ValueAt
    UnaryExpr.Recieve
  } > { left: // mul_op
    BinaryExpr.Product
    BinaryExpr.Quotient
    BinaryExpr.Remainder
    BinaryExpr.LeftShift
    BinaryExpr.RightShift
    BinaryExpr.BitwiseAnd
    BinaryExpr.BitClear
  } > { left: // add_op
    BinaryExpr.Sum
    BinaryExpr.Difference
    BinaryExpr.BitwiseOr
    BinaryExpr.BitwiseXor
  } > { left: // rel_op
    BinaryExpr.Equal
    BinaryExpr.NotEqual
    BinaryExpr.Less
    BinaryExpr.LessOrEqual
    BinaryExpr.Greater
    BinaryExpr.GreaterOrEqual
  } > { left: // and_op
    BinaryExpr.LogicalAnd
  } > { left: // or_op
    BinaryExpr.LogicalOr
  }

/**
 * Arithemtic operators -- https://golang.org/ref/spec#Arithmetic_operators
 */
 
  // no syntax

/**
 * Comparison operators -- https://golang.org/ref/spec#Comparison_operators
 */

  // no syntax

/**
 * Logical operators -- https://golang.org/ref/spec#Logical_operators
 */
 
  // no syntax

/**
 * Address operators -- https://golang.org/ref/spec#Address_operators
 */
 
  // no syntax

/**
 * Receive operators -- https://golang.org/ref/spec#Conversions
 */
 
  // no syntax

/**
 * Conversions -- https://golang.org/ref/spec#Conversions
 */
 
  context-free syntax 

  Conversion.Conversion = [[Type](t#[Expression] [Comma?])]
  Conversion.Conversion = <<Type>(<Expression> <Comma?>)>


/**
 * Constant expressions -- https://golang.org/ref/spec#Constant_expressions
 */
 
  context-free syntax 


/**
 * Order of evaluation -- https://golang.org/ref/spec#Order_of_evaluation
 */

  // no syntax
