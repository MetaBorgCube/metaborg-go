/**
 * Declarations and scope -- https://golang.org/ref/spec#Declarations_and_scope
 */
module Declarations

imports

  Blocks
  Lexical
  Expressions
  Types
  Statements
  SortNames

context-free start-symbols

  TopLevelDecl

context-free sorts TopLevelDecl
context-free syntax

  TopLevelDecl                = Declaration


/**
 * Constant declarations -- https://golang.org/ref/spec#Constant_declarations
 */
context-free sorts ConstSpec-ELEM ConstSpec IDList IDList-OPT
context-free syntax

  Declaration.SingleConstDecl = <const <ConstSpec>>
  Declaration.ConstDecl       = <const (<ConstSpec-ELEM*>)>
  ConstSpec-ELEM              = <<ConstSpec>;>

  // spec seems wrong here, assignment is always required
  ConstSpec.ConstSpec         = <<IDList> <Type-OPT> = <ExprList>>

  IDList                      = <<{ID ","}+>>
  IDList-OPT.NoIDs            = <>
  IDList-OPT                  = IDList

  ExprList                    = <<{Expr ","}+>>
  ExprList-OPT.NoExprs        = <>
  ExprList-OPT                = ExprList


/**
 * Type declarations -- https://golang.org/ref/spec#Type_declarations
 */
context-free sorts TypeSpec-ELEM TypeSpec
context-free syntax

  Declaration.SingleTypeDecl  = <type <TypeSpec>>
  Declaration.TypeDecl        = <type (<TypeSpec-ELEM*>)>
  TypeSpec-ELEM               = <<TypeSpec>;>

  TypeSpec.AliasDecl       		= <<ID> = <Type>>

  TypeSpec.TypeDef         		= <<ID> <Type>>


/**
 * Var declarations -- https://golang.org/ref/spec#Variable_declarations
 */
context-free sorts VarSpec-ELEM VarSpec
context-free syntax

  Declaration.SingleVarDecl   = <var <VarSpec>>
  Declaration.VarDecl         = <var (<VarSpec-ELEM*>)>
  VarSpec-ELEM                = <<VarSpec>;>

  VarSpec.VarSpec             = <<IDList> <Type> = <ExprList>>
  VarSpec.VarSpecNoInit       = <<IDList> <Type>>
  VarSpec.VarSpecNoType       = <<IDList> = <ExprList>>



/**
 * Function declarations -- https://golang.org/ref/spec#Function_declarations
 */
context-free syntax

  TopLevelDecl.FunctionDecl   = <func <ID> <Signature>>
  TopLevelDecl.FunctionDef    = <func <ID> <Signature> <Block>>


/**
 * Method declarations -- https://golang.org/ref/spec#Method_declarations
 */
context-free sorts Receiver Receiver-INNER
context-free syntax

  TopLevelDecl.MethodDecl     = <func <Receiver> <ID> <Signature>>
  TopLevelDecl.MethodDed      = <func <Receiver> <ID> <Signature> <Block>>

  // Inlined case where there is only one receiver, instead of Receiver = Parameters
  Receiver.Receiver           = <(<ID> <Receiver-INNER>)>
  Receiver.AnonymousReceiver  = <(<Receiver-INNER>)>
  Receiver-INNER.ReceiverType     = <<TypeName>>
  Receiver-INNER.ReceiverPtrType  = <*<TypeName>>

