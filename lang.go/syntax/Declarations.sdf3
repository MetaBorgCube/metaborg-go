module Declarations // https://golang.org/ref/spec#Declarations_and_scope

imports

  Blocks
  Lexical
  Expressions
  Types

context-free start-symbols

  TopLevelDecl

context-free syntax

  TopLevelDecl = Declaration

/**
 * Constant declarations -- https://golang.org/ref/spec#Constant_declarations
 */
context-free syntax

  Declaration.SingleConstDecl      = <const <ConstSpec>>
  Declaration.MultiConstDecl       = <const (<ConstSpec-ELEM*>)>
  ConstSpec-ELEM.ConstSpec-ELEM    = <<ConstSpec>;>

  // spec seems wrong here, assignment is always required
  ConstSpec.ConstSpec              = <<IDList> <Type-OPT> = <ExprList>>

  IDList                           = {ID ","}+
  IDList-OPT.NoIdentifiers         = <>
  IDList-OPT                       = IDList

  ExprList                   = {Expr ","}+
  ExprList-OPT.NoExprs = <>
  ExprList-OPT               = ExprList

/**
 * Iota -- https://golang.org/ref/spec#Iota
 */
context-free syntax

/**
 * Type declarations -- https://golang.org/ref/spec#Type_declarations
 */
context-free syntax

  Declaration.SingleTypeDecl   		= <type <TypeSpec>>
  Declaration.MultiTypeDecl       	= <type (<TypeSpec-ELEM*>)>
  TypeSpec-ELEM.TypeSpec-ELEM     	= <<TypeSpec>;>

  TypeSpec.AliasDecl       			= <<ID> = <Type>>

  TypeSpec.TypeDef         			= <<ID> <Type>>


/**
 * Var declarations -- https://golang.org/ref/spec#Variable_declarations
 */
context-free syntax


  Declaration.SingleVarDecl      = <var <VarSpec>>
  Declaration.MultiVarDecl       = <var (<VarSpec-ELEM*>)>
  VarSpec-ELEM.VarSpec-ELEM      = <<VarSpec>;>

  // ERROR: when both Type _and_ VarSpec-Assign are not specified.
  VarSpec.VarSpec                = <<IDList> <Type-OPT> <VarSpec-Assign-OPT>>

  VarSpec-Assign-OPT.NoVarAssign = <>
  VarSpec-Assign-OPT.VarAssign   = <= <ExprList>>


/**
 * Short variable declarations -- https://golang.org/ref/spec#Short_variable_declarations
 */
context-free syntax

  ShortVarDecl.ShortVarDecl = <<IDList> := <ExprList>>


/**
 * Function declarations -- https://golang.org/ref/spec#Function_declarations
 */
context-free syntax

  TopLevelDecl.FunctionDecl = <func <ID> <FunctionDecl-Inner>>

  FunctionDecl-Inner        = Signature
  FunctionDecl-Inner        = Function

  Function.Function         = <<Signature> <Block>>


/**
 * Method declarations -- https://golang.org/ref/spec#Method_declarations
 */
context-free syntax

  TopLevelDecl.MethodDecl = <func <Receiver> <MethodName> <MethodDecl-Inner>>
  MethodDecl-Inner      = Signature
  MethodDecl-Inner      = Function


//Receiver.Receiver          = Parameters                   // FIXME inlined special case here
  Receiver.Receiver          = <(<ID> <Receiver-Inner>)>
  Receiver.AnonymousReceiver = <(<Receiver-Inner>)>
  Receiver-Inner             = TypeName
  Receiver-Inner             = <*<TypeName>>
