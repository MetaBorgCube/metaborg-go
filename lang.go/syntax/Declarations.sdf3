module Declarations // https://golang.org/ref/spec#Declarations_and_scope

imports

  Blocks
  Lexical
  Expressions
  Types

context-free start-symbols

  TopLevelDecl

context-free syntax

  Declaration  = ConstDecl
  Declaration  = TypeDecl
  Declaration  = VarDecl

  TopLevelDecl = Declaration
  TopLevelDecl = FunctionDecl
  TopLevelDecl = MethodDecl

/**
 * Constant declarations -- https://golang.org/ref/spec#Constant_declarations
 */
 
  context-free syntax
  
  ConstDecl       = <const <ConstDecl-Inner>>
  ConstDecl-Inner = ConstSpec
  ConstDecl-Inner = <(<{ConstSpec ";"}*>)>
  
  ConstSpec       = <<IdentifierList> <ConstSpec-Inner?>>
  ConstSpec-Inner = <<Type?> = <ExpressionList>>
  
  IdentifierList  = {Identifier ","}*
  ExpressionList  = {Expression ","}+

/**
 * Iota -- https://golang.org/ref/spec#Iota
 */
 
/**
 * Type declarations -- https://golang.org/ref/spec#Type_declarations
 */
 
  context-free syntax
  
  TypeDecl       = <type <TypeDecl-Inner>>
  TypeDecl-Inner = TypeSpec
  TypeDecl-Inner = <(<{TypeSpec ";"}*>)>

  TypeSpec       = Identifier Type

  
  VarDecl = []
  FunctionDecl = []
  MethodDecl = []

/**
 * Var declarations -- https://golang.org/ref/spec#Variable_declarations
 */
 
  VarDecl        = <var <VarDecl-Inner>>
  VarDecl-Inner  = VarSpec
  VarDecl-Inner  = <(<{VarSpec ";"}*>)>
  
  VarSpec        = IdentifierList VarSpec-Inner
  VarSpec-Inner  = <<Type> <VarSpec-Assign?>>
  VarSpec-Inner  = VarSpec-Assign
  VarSpec-Assign = <= <ExpressionList>>

  
/**
 * Short variable declarations -- https://golang.org/ref/spec#Short_variable_declarations
 */
 
  context-free syntax

  ShortVarDecl = <<IdentifierList> := <ExpressionList>>


/**
 * Function declarations -- https://golang.org/ref/spec#Function_declarations
 */
 
  context-free syntax
  
  FunctionDecl       = <func <FunctionName> <FunctionDecl-Inner>>
  FunctionDecl-Inner = Function
  FunctionDecl-Inner = Signature
  
  FunctionName       = Identifier
  Function           = Signature FunctionBody
  FunctionBody       = Block


/**
 * Method declarations -- https://golang.org/ref/spec#Method_declarations
 */
 
  context-free syntax
  
  MethodDecl       = <func <Receiver> <MethodName> <MethodDecl-Inner>>
  MethodDecl-Inner = Function
  MethodDecl-Inner = Signature

  Receiver         = Parameters
