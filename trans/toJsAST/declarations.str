module toJsAST/declarations

imports 
	signatures/-
	signatures/spoofaxJS-sig
	signatures/MethodDecl-sig
	signatures/Parameter-sig
	signatures/Statement-sig
	signatures/Expression-sig
	signatures/Common-sig
	trans/pp
	toJsAST/statements
	helpers
		
rules
	
	gen-js-ast : FunctionDecl(
		FunctionName(name),
		Function(
			Signature(Parameters(params), returnType),
			FunctionBody(block)
		)
	) -> method(name, paramsJs, bodyJs)
		where
			debug(!<concat-strings> ["Found function ", name, " with parameters ", <write-to-string> params]);
			paramsJs := <map(gen-js-ast)> params;
			bodyJs := <func-block> block
	
	gen-js-ast : ParameterDecl(name, _) -> parameter(name)
	
	gen-js-ast : ConstDecl(constDecl) -> <gen-js-ast> constDecl
	gen-js-ast : VarDecl(varDecl) -> <gen-js-ast> varDecl
	
	// Ignore type
	gen-js-ast : VarSpecTyped(IdentifierList(identifierList), _, None()) -> <map(to-js-var)> identifierList
	gen-js-ast : ConstSpecTyped(IdentifierList(identifierList), _, None()) -> <map(to-js-const)> identifierList
	
	to-js-var : identifier -> varDecl(identifier, Null())
	to-js-const : identifier -> constDecl(identifier, Null())
	
	gen-js-ast : VarSpec(IdentifierList(targets), VarDeclAssign(ExpressionList(values))) -> declarationsJs
		with
			debug(!<concat-strings> ["Targets ", <write-to-string> targets]);
			debug(!<concat-strings> ["Values ", <write-to-string> values]);
			declarationsJs := <zip(to-js-vars)> (targets, values)
	
	gen-js-ast : ConstSpec(IdentifierList(targets), ConstDeclAssign(ExpressionList(values))) -> declarationsJs
		with
			debug(!<concat-strings> ["Targets ", <write-to-string> targets]);
			debug(!<concat-strings> ["Values ", <write-to-string> values]);
			declarationsJs := <zip(to-js-consts)> (targets, values)
	
	to-js-vars: (target, value) -> varDecl(target, valueJs)
		with
			valueJs := <gen-js-ast> value
	
	to-js-consts: (target, value) -> constDecl(target, valueJs)
		with
			valueJs := <gen-js-ast> value
			
	gen-js-ast : ShortVarDecl(IdentifierList(targets), ShortVarDeclAssign(ExpressionList(values))) -> declarationsJs
		with
			declarationsJs := <zip(to-js-vars)> (targets, values)