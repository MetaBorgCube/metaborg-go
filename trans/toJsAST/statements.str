module toJsAST/statements

imports 
	signatures/-
	signatures/spoofaxJS-sig
	signatures/MethodDecl-sig
	signatures/Parameter-sig
	signatures/Statement-sig
	signatures/Expression-sig
	signatures/Common-sig
	toJsAST/channelhelper
	toJsAST/expressions
	trans/pp
	helpers
	
		
rules
	
	func-block : Block(statementList) -> [try(
		<conc> ([deferStackDecl], statementsJs), 
		catch(Identifier("e"), [executeDeferred], None())
	)
	]
		where
			deferstackname := <debug(!"HOOI"); newname> "___deferstack___";
			deferStackDecl := varDecl(deferstackname, Call(Property(Identifier(GOROUTINE_LIBNAME), GODEFER_NEWDEFERMETHOD), []));
			statements := <debug(!"HAAI"); statements-from-list> statementList;
			executeDeferred := <get-deferstmt(|deferstackname)> [];
			statementsWithAddedJs := <conc; debug(!"Concat")> (statements, [AddedJsStmt(executeDeferred)]);
			statementsJs := <debug(!"Block"); stmt-iter(|deferstackname); debug(!"Finished STMT Iter")> statementsWithAddedJs	
			//TODO: Add Cleanup on return
	
	gen-js-ast : Block(statementList) -> js
		where
			statements := <statements-from-list> statementList;
			js := <stmt-iter(|"")> statements 
	
	
	//Send Statement, Generate Callback
	stmt-iter(|defername): [SendStmt(channel, exp) | xs] ->[
		expStmt(
			Call(
				Property(chanjs, "send"),[ 
				expjs, 
				AnonFunction([], [try(remaining, catch(Identifier("e"), executeDeferred, None()))])
               ]
            )
        )]
		where
			chanjs := <gen-js-ast> channel;
			expjs := <gen-js-ast> exp;
			remaining := <stmt-iter(|defername)> xs;
			executeDeferred := [<get-deferstmt(|defername)> []]
	
	
	//Call(Property(Identifier("console"),"log"),[String("\"test\"")])
	//To simlify it only (anonymous) functions are allowed;
	stmt-iter(|defername): [DeferStmt(exp) | tail] -> <concat> [[addStmt], paramStmt, <stmt-iter(|defername)> tail]
		where
			expJs := <gen-js-ast; debug(!"ExpInJS")> exp;
			addStmt := expStmt(Call(Property(Identifier(defername), "add"), [<debug(!"FuncNamePre"); funcname; debug(!"FuncNamePost")> expJs]));
			paramStmt :=  <debug(!"ParPre"); params; map(gen-addparamstmt(|defername))> expJs
			 
	funcname: Call(Property(ident, p), params) -> Property(ident, p)
	funcname: Call(x, params) -> Identifier(x)
	
	params: Call(funcname, params) -> params
	gen-addparamstmt(|defername): x -> expStmt(Call(Property(Identifier(defername), GODEFER_ADDPARAMMETHOD), [x]))
	
	//Receive Expressions in Expression
	//Untangle the receive expressions, generate extra statements
	//and then execute this statement
	stmt-iter(|defername): [stmt | tail] -> stmtJs
		where
			blockingStmts := <collect-om(?SubstitutedExp(_, _, _)); debug(!"Blocking Set: ")>  stmt;
			<not-empty> blockingStmts;
			extraStatements := <mapconcat(extract-stmtlist)> blockingStmts;
			stmtJs := <generate-receive(|stmt, tail, defername)> extraStatements 

	//Normal  non-blocking statement
	stmt-iter(|defername): [stmt | tail] -> <conc> (allStmtJs, xsjs)
		where
			blockingStmts := <collect-om(?SubstitutedExp(_, _, _)); debug(!"Empty Blocking Set: ")>  stmt;
			<equal>(blockingStmts, []);
			xjs := <gen-js-ast> stmt;
			allStmtJs := <conc> (<cleanup-stmts(|defername)> stmt, xjs);
			xsjs := <stmt-iter(|defername)> tail
	
	
	cleanup-stmts(|deferstackname): ReturnStmt(_) -> [<get-deferstmt(|deferstackname)> ""]
	cleanup-stmts(|deferstackname): x -> []
	
	get-deferstmt(|name): x -> []
		where
			<equal>(name, "")
	
	get-deferstmt(|defername): x -> expStmt(Call(Property(Identifier(defername), GODEFER_EXECECUTEMETHOD), []))		
	
	//Empty 
	stmt-iter(|defername): [] -> []
	
	not-empty: [x | xs] -> []
	
	gen-js-ast: AddedJsStmt(stmt) -> [stmt]
		with
			<debug(!"Mathced")> stmt
	
	gen-js-ast:
		ExpressionStmt(expression) -> [expStmt(<gen-js-ast> expression)]
	
	// Matches on functions returning (possible) multiple values
	gen-js-ast:
		Assignment(
			ExpressionList(targetExpressions),
			AssignEquals(),
			ExpressionList([singleValueExpression])
		) -> js
	      where
	      	<gt> (<length> targetExpressions, 1);
			valuesArray := <gen-js-ast> singleValueExpression;
			js := <gen-js-ast-multiple-assignment> (valuesArray, targetExpressions)
	
	gen-js-ast:
		Assignment(
			ExpressionList(targetExpressions),
			AssignEquals(),
			ExpressionList(valueExpressions)
		) -> js
	      where
			js := <zip(to-js-equals-assign)> (targetExpressions, valueExpressions)
	
	gen-js-ast-multiple-assignment:
		(valuesArray, targetExps) -> <flatten-list> [[valuesArrayJs], assignmentsJs]
		where
			tmpIdentifier := <strcat> ("tmp_assign_", <int-to-string> <abs> <next-random>);
			valuesArrayJs := varDecl(tmpIdentifier, valuesArray);
			assignmentsJs := <map(gen-js-ast-indexed-assign(|tmpIdentifier))> <add-indices> targetExps
	
	to-js-equals-assign: (UnaryExpr(UnaryExpr("*", operand)), rhs) 
		-> expStmt(Call(Property(operandJs, POINTERLIB_SET), [rhJs]))
		where
			rhJs := <gen-js-ast> rhs;
			operandJs := <gen-js-ast> operand

	to-js-equals-assign:
		(lhs, rhs) -> assignment(lhsJs, rhsJs)
		where
			lhsJs := <gen-js-ast> lhs;
			rhsJs := <gen-js-ast> rhs

	gen-js-ast-indexed-assign(|tmpIdentifier): (index, UnaryExpr(UnaryExpr("*", operand))) 
	-> expStmt(Call(Property(operandJs, POINTERLIB_SET), [array])) 
		where
			operandJs := <gen-js-ast> operand;
			array := Array(Identifier(tmpIdentifier), IntegerLiteral(<int-to-string> <int-dec> index))
			
	gen-js-ast-indexed-assign(|tmpIdentifier): (index, lhs) -> assignment(lhsJs, Array(Identifier(tmpIdentifier), IntegerLiteral(<int-to-string> <int-dec> index)))
		where
			lhsJs := <gen-js-ast> lhs
			
	gen-js-ast:
		GoStmt(exp) -> [expStmt(Call(Property(Identifier(GOROUTINE_LIBNAME), GOROURTINE_GOMETHOD),
			[AnonFunction([], [expStmt(expJs)])]
		))]
		where
			expJs := <gen-js-ast> exp
			
	gen-js-ast:
		ReturnStmt(None()) -> [return(Undefined())]
	
	gen-js-ast:
		ReturnStmt(Some(expList)) -> [return(arrayJs)]
		where
			arrayJs := <exps-list-to-js-array> expList
	
	exps-list-to-js-array:
		ExpressionList(exps) -> ArrayLiteral(expsJs)
		where
			expsJs := <map(gen-js-ast)> exps

overlays 
	
	GOROUTINE_LIBNAME = "routine"
	GOROURTINE_GOMETHOD = "go"
	GOROUTINE_MAKECHANNELMETHOD = "newChannel"
	
	GODEFER_NEWDEFERMETHOD = "newDeferList"
	GODEFER_ADDPARAMMETHOD = "addParam"
	GODEFER_ADDDEFERMETHOD = "add"
	GODEFER_EXECECUTEMETHOD = "cleanUp"
	
	ARRAYLIB_NAME = "__GO_ARRAYLIB__"
	ARRAYLIB_NEWARRAYMETHOD = "newArray" 
	ARRAYLIB_LOOKUPMETHOD = "getVal"
	ARRAYLIB_CREATESLICEMETHOD = "getSlice"
	ARRAYLIB_GETSTARTPOSTMETHOD = "getStartPos"
	
	POINTERLIB_NAME = "__GO_POINTERLIB__"
	POINTERLIB_NEWPOINTER = "newPointer"
	POINTERLIB_GET = "get"
	POINTERLIB_SET = "set"
	
